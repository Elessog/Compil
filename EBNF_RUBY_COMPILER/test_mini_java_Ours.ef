letterDef = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g"
       | "h" | "i" | "j" | "k" | "l" | "m" | "n"
       | "o" | "p" | "q" | "r" | "s" | "t" | "u"
       | "v" | "w" | "x" | "y" | "z" ;
digitDef = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
symbolDef = "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">"
       | "'" | """ | "=" | "|" | "." | "," | ";" | "&" | "*" | "+" | "-" | "!" | "&&";


Goal 	= 	MainClass, { ClassDeclaration }, EOF;
MainClass 	= 	"class", Identifier, "{", "public", "static", "void", "main", "(", "String", "[", "]", Identifier, ")", "{", Statement, "}", "}";
ClassDeclaration 	= 	"class", Identifier, [ "extends", Identifier ], ["implements",Identifier],"{", { VarDeclaration }, { MethodDeclaration }, "}";
VarDeclaration 	= 	Type, Identifier, ";";
MethodDeclaration 	= 	"public", Type, Identifier, "(", [ Type, Identifier, { ",", Type, Identifier }], ")", "{", { VarDeclaration }, { Statement }, "return", Expression, ";", "}";
Type 	= 	("int", "[", "]")
	| 	("boolean")
	| 	("int")
	| 	(Identifier)
	; 	
Statement 	= 	("{", { Statement }, "}")
	| 	("if", "(", Expression, ")", Statement, "else", Statement)
	| 	("while", "(", Expression, ")", Statement)
	| 	("System.out.println", "(" , Expression, ")", ";")
	| 	(Identifier, "=", Expression, ";")
	| 	(Identifier, "[", Expression, "]", "=", Expression, ";")
	; 	
Expression 	= (IntegerLiteral)
	| 	("true")
	| 	("false")
	| 	(Identifier)
	| 	("this")
	| 	("new", "int", "[", Expression, "]")
	| 	("new", Identifier ,"(" ,")")
	| 	("!", Expression)
	| 	("(", Expression, ")")
	| 	(Expression, "[", Expression, "]")
	| 	(Expression, ".", "length")
	| 	(Expression, ".", Identifier, "(", [ Expression , { ",", Expression } ], ")")
	|	(Expression , ( "&&" | "<"| ">" | "+" | "-" | "*" ) , Expression)
	; 	
EOF  = "EOF";

